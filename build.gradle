plugins {
    id("org.springframework.boot") version "3.3.4" apply false
    id("io.spring.dependency-management") version "1.1.4" apply false
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply(plugin: "java-library")
    apply(plugin: "io.spring.dependency-management")

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    group = "com.janwee.bookstore"
    version = "0.0.1-SNAPSHOT"

    configurations.configureEach {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }

    def integrationTest = sourceSets.create("integrationTest") {
        java.srcDir("src/integrationTest/java")
        resources.srcDir("src/integrationTest/resources")
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }

    configurations {
        compileOnly.extendsFrom(annotationProcessor)
        integrationTestImplementation.extendsFrom(testImplementation)
        integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
    }

    tasks.register("integrationTest", Test) {
        description = "Runs the integration tests."
        group = "verification"
        testClassesDirs = integrationTest.output.classesDirs
        classpath = integrationTest.runtimeClasspath
        mustRunAfter(tasks.named("test"))
    }

    dependencies {
        implementation("org.ow2.asm:asm:9.6")
        implementation("org.springframework.boot:spring-boot-starter-log4j2")

        annotationProcessor("org.projectlombok:lombok")

        testImplementation("org.junit.jupiter:junit-jupiter:5.10.3")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.10.3")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")

        integrationTestImplementation(project)
        integrationTestImplementation("org.junit.jupiter:junit-jupiter:5.10.3")
        integrationTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.10.3")
        integrationTestRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:3.3.4")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:2023.0.3")
        }

        dependencies {
            dependency("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0")
            dependency("org.hibernate.orm:hibernate-processor:7.0.0.Alpha3")
            dependency("org.postgresql:postgresql:42.6.0")
            dependency("jakarta.servlet:jakarta.servlet-api:6.1.0")
            dependency("org.mockito:mockito-core:5.19.0")
            dependency("org.mockito:mockito-junit-jupiter:5.19.0")
            dependency("org.projectlombok:lombok:1.18.24")
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

tasks.register("hello") {
    doLast {
        println("Hello!")
    }
}

tasks.register("greet") {
    doLast {
        println("How are you?")
    }
//    dependsOn("hello")
}

tasks.named("hello").configure {
//    mustRunAfter(tasks.named("hello"))
//    shouldRunAfter(tasks.named("hello"))
    finalizedBy(tasks.named("greet"))
}